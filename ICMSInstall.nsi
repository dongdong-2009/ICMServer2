; Script generated by the HM NIS Edit Script Wizard.
;!include "DotNetChecker.nsh"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "ControlServer"
;x.x.1 is with camera ,x.x.0 is with out camera
!define VERSION 7.1.1.1
!define COMPANY ITE
;!define PRODUCT_VERSION "4.3.0_%Y%m%d_R_NANYI"
!define /date PRODUCT_VERSION "${VERSION}_%Y%m%d_${COMPANY}_R"
!define PRODUCT_PUBLISHER "${COMPANY}"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\ICMServer.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
;!define MUI_FINISHPAGE_RUN "$INSTDIR\ICMServer.exe"
;!define MUI_FINISHPAGE_RUN "$INSTDIR\DBtool.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "SimpChinese"
!insertmacro MUI_LANGUAGE "TradChinese"

LangString language ${LANGUAGE_SIMPCHINESE} "chs"
LangString language ${LANGUAGE_TRADCHINESE} "chc"
LangString language ${LANGUAGE_ENGLISH} "eng"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
!define /date TIMESTAMP "%Y%m%d_%H%M"
OutFile "ICMSSetup_${VERSION}_${TIMESTAMP}.exe"
InstallDir "$PROGRAMFILES\ICMServer"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
;  !insertmacro CheckNetFramework 451
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer

  ; Uninstall previous version
  ExecWait '"$INSTDIR\uninstaller.exe" /S _?=$INSTDIR'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Visual Studio 2013 redistributable package
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Check if Visual Studio 2013 redistributable package is already installed
  ReadRegDWORD $R0 HKLM "SOFTWARE\Microsoft\DevDiv\vc\Servicing\12.0\RuntimeMinimum" "Install"
  IntCmp $R0 1 VC2013RedistIsInstalled +1
  ; if Visual Studio 2013 redistributable package is not installed
  ; install Visual Studio 2013 redistributable package
  File "..\Preinstall\vcredist_x86.exe"
  ExecWait "vcredist_x86.exe /install /quiet /norestart"
  Delete vcredist_x86.exe

VC2013RedistIsInstalled:
  DetailPrint "Visual Studio 2013 redistributable package is already installed"
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; .NET Framework 4.5.1 redistributable package
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Check if .NET Framework 4.5.1 is already installed
  ReadRegDWORD $R0 HKLM "SOFTWARE\Microsoft\NET Framework Setup\NDP\v4\Full" "Release"
  IntCmp $R0 378675 DotNET451IsInstalled +1 DotNET451IsInstalled
  ; if .NET Framework 4.5.1 is not installed
  File "..\Preinstall\NDP451-KB2858728-x86-x64-AllOS-ENU.exe"
  ExecWait "NDP451-KB2858728-x86-x64-AllOS-ENU.exe /passive /norestart"
  Delete "NDP451-KB2858728-x86-x64-AllOS-ENU.exe"

DotNET451IsInstalled:
  DetailPrint ".NET Framework 4.5.1 is already installed"
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Database
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Check to see if MariaDB 10.1 is already installed
  ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\MariaDB 10.1" "UninstallString"
  StrCmp $R0 "" +1 DBIsInstalled
  ; if MariaDB 10.1 is not installed
  ; Check to see if MySQL 5.6 is already installed
  ReadRegStr $R0 HKLM "SOFTWARE\MySQL AB\MySQL Server 5.6" "Version"
  StrCmp $R0 "" +1 DBIsInstalled
  ; if MySQL Server 5.6 is not installed
  ; Install MariaDB
  File "..\Preinstall\mariadb-10.1.21-win32.msi"
  ExecWait "msiexec.exe /i mariadb-10.1.21-win32.msi SERVICENAME=MySQL PASSWORD=123456 UTF8=1 /qn"
  Delete "mariadb-10.1.21-win32.msi"

DBIsInstalled:
  DetailPrint "MariaDB is already installed"
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MySQL Connector/ODBC 5.1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Check if MySQL Connector/ODBC 5.1 is already installed
  ReadRegDWORD $R0 HKLM "SOFTWARE\MySQL AB\MySQL Connector/ODBC 5.1" "Version"
  StrCmp $R0 "" +1 MySQLODBCIsInstalled
  ; if MySQL Connector/ODBC 5.1 is not installed
  File "..\Preinstall\mysql-connector-odbc-5.1.6-win32.msi"
  ExecWait "msiexec.exe /i mysql-connector-odbc-5.1.6-win32.msi /qn"
  Delete "mysql-connector-odbc-5.1.6-win32.msi"

MySQLODBCIsInstalled:
  DetailPrint "MySQL Connector/ODBC 5.1"
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  File "bin\Release\*.manifest"
  ;dll
  File "bin\Release\*.dll"
  ;other
  File "bin\Release\mm.cfg"
  File "bin\Release\*.exe"
  File "bin\Release\*.sql"
  File "bin\Release\*.config"

  CreateDirectory "$SMPROGRAMS\ICMServer"
  CreateShortCut "$SMPROGRAMS\ICMServer\ICMServer.lnk" "$INSTDIR\ICMServer.exe"
  CreateShortCut "$DESKTOP\ICMServer.lnk" "$INSTDIR\ICMServer.exe"

  ExecWait "DBtool.exe"
SectionEnd

Section "ResSection" SEC02
  SetOutPath "$INSTDIR\res\"
  SetOverwrite ifnewer
  ;RES
  File "bin\Release\res\*"
SectionEnd

Section "LanguageSection" SEC03
  SetOutPath "$INSTDIR\zh-CN\"
  SetOverwrite ifnewer
  File "bin\Release\zh-CN\*"
SectionEnd

;Section "CulturesSection" SEC04
;  SetOutPath "$INSTDIR\Cultures\"
;  SetOverwrite ifnewer
;  File "bin\Release\Cultures\*"
;SectionEnd

;Section "ImagesSection" SEC05
;  SetOutPath "$INSTDIR\Images\"
;  SetOverwrite ifnewer
;  File "bin\Release\Images\*"
;SectionEnd

Section
WriteINIStr $INSTDIR\mm.cfg "SECTION_CONFIG" "FTPDIR" $INSTDIR
WriteINIStr $INSTDIR\mm.cfg "AddrBook" "INDEX_SYS_VERSION" "${PRODUCT_VERSION}"
WriteINIStr $INSTDIR\mm.cfg "AddrBook" "INDEX_SYS_LANGUAGE" $(Language)
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\ICMServer\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\ICMServer.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\ICMServer.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) Has been successfully removed from your computer."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove the $(^Name), and all components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  RMDir /r "$INSTDIR\Cultures\"
  RMDir /r "$INSTDIR\Images\"
  RMDir /r "$INSTDIR\logs\"
  RMDir /r "$INSTDIR\res\"
  RMDir /r "$INSTDIR\zh-CN\"

  Delete "$INSTDIR\*.config"
  Delete "$INSTDIR\*.dll"
  Delete "$INSTDIR\*.exe"
  Delete "$INSTDIR\*.manifest"
  Delete "$INSTDIR\*.sql"
  Delete "$INSTDIR\mm.cfg"
  Delete "$DESKTOP\ICMServer.lnk"
  Delete "$INSTDIR\FileZilla Server.xml"

  Delete "$DESKTOP\ICMServer.lnk"
  SetShellVarContext current
  RMDir /r /REBOOTOK "$SMPROGRAMS\ICMServer"
  SetShellVarContext all

  Delete "$SMPROGRAMS\ICMServer\ICMServer.lnk"
  Delete "$SMPROGRAMS\ICMServer\Uninstall.lnk"


  ;RMDir /r /REBOOTOK "$SMPROGRAMS\ICMServer"
  ;RMDir /r /REBOOTOK "$INSTDIR\data"
  ;RMDir /r /REBOOTOK "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKCU "${PRODUCT_DIR_REGKEY}"

  SetAutoClose true
SectionEnd